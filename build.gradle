plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'com.palantir.docker' version '0.25.0'
	id "com.palantir.docker-run" version "0.25.0"
	id "com.palantir.docker-compose" version "0.25.0"
}

group = 'pho.formulaone.web'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'junit:junit:4.12'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "org.mockito:mockito-junit-jupiter:3.2.4"
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.2"
	testImplementation "io.cucumber:cucumber-java:5.5.0"
	testImplementation "io.cucumber:cucumber-spring:5.5.0"
	testImplementation "io.cucumber:cucumber-junit:5.5.0"
	testImplementation "net.masterthought:cucumber-reporting:4.2.3"
	testCompile "org.testcontainers:postgresql:1.14.3"
	testImplementation "org.testcontainers:junit-jupiter:1.12.3"
	testCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.3'
	testImplementation "org.mockito:mockito-junit-jupiter:3.6.0"
	implementation "org.mapstruct:mapstruct:1.4.1.Final"
	annotationProcessor "org.mapstruct:mapstruct-processor:1.4.1.Final"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.4.1.Final"
}

test {
	filter {
		excludeTestsMatching  "*CucumberTest*"
	}
	useJUnitPlatform {
		excludeTags 'behaviour'
	}
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/docker/dependency")
}

docker {
	tag 'local', 'functional-formula-one-web:latest'
	tag 'localVersion', "functional-formula-one-web:${version}"
	name 'functional-formula-one-web'
	buildArgs([DEPENDENCY: "dependency", APPCLASS: 'pho.formulaone.web.FunctionalFormulaOneWebApplication'])
	dockerfile file("Dockerfile")
}

dockerCompose {
	template 'src/main/docker/docker-compose.functionalformulaoneweb.yml'
	dockerComposeFile "$buildDir/docker/ortolanuniverse/docker-compose.yml"
	templateTokens([
			'ffoneweb.version': "$project.version",
	])
}

dockerRun {
	name = "functional-formula-one-web-main"
	image = "functional-formula-one-web"
	ports("8080:8080")
}

dockerPrepare.dependsOn unpack

configure(unpack) {
	group = 'docker'
	description = 'Unpacks the boot jar so we can build multi-layer images'
}

task cucumber(type: Test){
	filter {
		includeTestsMatching  "*CucumberTest*"
	}
	check.dependsOn it
	shouldRunAfter test
}

configure(cucumber) {
	group = 'verification'
	description = 'Runs the Cucumber Tests'
}

task onlyMe(type: Test){
	systemProperty  "CUCUMBER_FILTER_TAGS", "@onlyMe"
	filter {
		includeTestsMatching  "*CucumberTest*"
	}
	shouldRunAfter test
}

configure(onlyMe) {
	group = 'verification'
	description = 'Runs only a subset of the cucumber tests marked with @onlyMe tag. Useful for debugging'
}

task behaviourTest(type: Test){
	useJUnitPlatform {
		includeTags 'behaviour'
	}
	check.dependsOn it
	shouldRunAfter test
}

configure(behaviourTest) {
	group = 'verification'
	description = 'Runs the Behavioural Tests'
}
