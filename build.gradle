plugins {
	id 'org.springframework.boot' version '2.6.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java-library'
	id 'com.palantir.docker' version '0.27.0'
	id "com.palantir.docker-run" version "0.27.0"
	id "com.palantir.docker-compose" version "0.27.0"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}

group = 'pho.formulaone.web'
version = '0.0.1-SNAPSHOT'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'
	implementation 'org.springframework.boot:spring-boot-starter-web:2.6.4'
	implementation 'org.projectlombok:lombok:1.18.22'
	compileOnly 'org.projectlombok:lombok:1.18.22'
	runtimeOnly 'org.postgresql:postgresql:42.3.3'
	annotationProcessor 'org.projectlombok:lombok:1.18.22'
	testImplementation('org.springframework.boot:spring-boot-starter-test:2.6.4') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "org.mockito:mockito-junit-jupiter:4.4.0"
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
	testImplementation "io.cucumber:cucumber-java:7.2.3"
	testImplementation "io.cucumber:cucumber-spring:7.2.3"
	testImplementation 'io.cucumber:cucumber-junit:7.2.3'
	testImplementation "net.masterthought:cucumber-reporting:5.6.1"
	testImplementation "org.testcontainers:postgresql:1.16.3"
	testImplementation "org.testcontainers:junit-jupiter:1.16.3"
	testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
	testImplementation "org.mockito:mockito-junit-jupiter:4.4.0"
	implementation "org.mapstruct:mapstruct:1.4.2.Final"
	annotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.4.2.Final"
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
}

test {
	filter {
		excludeTestsMatching  "*CucumberTest*"
	}
	useJUnitPlatform {
		excludeTags 'behaviour'
	}
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/docker/dependency")
}

docker {
	tag 'local', 'functional-formula-one-web:latest'
	tag 'localVersion', "functional-formula-one-web:${version}"
	name 'functional-formula-one-web'
	buildArgs([DEPENDENCY: "dependency", APPCLASS: 'pho.formulaone.web.FunctionalFormulaOneWebApplication'])
	dockerfile file("Dockerfile")
}

dockerCompose {
	template 'src/main/docker/docker-compose.functionalformulaoneweb.yml'
	dockerComposeFile "$buildDir/docker/ortolanuniverse/docker-compose.yml"
	templateTokens([
			'ffoneweb.version': "$project.version",
	])
}

dockerRun {
	name = "functional-formula-one-web-main"
	image = "functional-formula-one-web"
	ports("8080:8080")
}

dockerPrepare.dependsOn unpack

configure(unpack) {
	group = 'docker'
	description = 'Unpacks the boot jar so we can build multi-layer images'
}

task cucumber(type: Test){
	filter {
		includeTestsMatching  "*CucumberTest*"
	}
	check.dependsOn it
	shouldRunAfter test
}

configure(cucumber) {
	group = 'verification'
	description = 'Runs the Cucumber Tests'
}

task onlyMe(type: Test){
	systemProperty  "CUCUMBER_FILTER_TAGS", "@onlyMe"
	filter {
		includeTestsMatching  "*CucumberTest*"
	}
	shouldRunAfter test
}

configure(onlyMe) {
	group = 'verification'
	description = 'Runs only a subset of the cucumber tests marked with @onlyMe tag. Useful for debugging'
}

task behaviourTest(type: Test){
	useJUnitPlatform {
		includeTags 'behaviour'
	}
	check.dependsOn it
	shouldRunAfter test
}

configure(behaviourTest) {
	group = 'verification'
	description = 'Runs the Behavioural Tests'
}
